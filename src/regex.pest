character = { 'a'..'z' | 'A'..'Z' | '0'..'9' }

whitespace = _{ " " | "\n" }

range = { character ~ "-" ~ character }

set = { (range | character)+ }

op_not = { "^" }

reverse_set = { op_not? ~ set }

brackets = _{ "(" ~ choose ~ ")" |
  "[" ~ reverse_set ~ "]" | character }

empty = { "" }

op_repeat = { "*" }
op_plus = { "+" }
op_optional = { "?" }
repeat_suffix = _{ op_repeat | op_plus | op_optional }

repeat = { brackets ~ repeat_suffix | brackets }

concat = { repeat ~ concat | repeat }

concat_or_empty = _{ concat | empty }

choose = { concat_or_empty ~ "|" ~ choose | concat_or_empty }

regex = _{ soi ~ choose ~ eoi }


